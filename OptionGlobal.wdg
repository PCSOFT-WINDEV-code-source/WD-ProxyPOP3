#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : OptionGlobal
 major_version : 28
 minor_version : 0
 type : 7
 description : ""
 subtype : 0
procedure_set :
 identifier : 0x3f0a76532d575b8b
 internal_properties : CAAAAAgAAABGLu41kG7fjQV3iS4F72qmnKaNh5694reolNKIW0iw
 code_elements :
  type_code : 31
  p_codes :
   -
     code : |1+
      stOption est une structure
      	m_sNomOption est une chaine
      	m_valOption est un variant
      FIN
      
      gsCleOption est une chaine // Clé de registre pour la sauvegarde des options
      gptabOption est un tableau dynamique = allouer un tableau de 0 stOption // Table des options
      gnnbOption est un entier = 0 // Nombre d'options
     type : 720896
   -
     code : |1-
      
      liberer gptabOption
      gnnbOption = 0
     type : 720898
  procedures :
   -
     name : DeclareOption
     procedure_id : 4542573455538127348
     type_code : 15
     code : |1+
      // Syntaxe :
      //DeclareOption (<sNom> est chaîne, <defVal>)
      //
      // Paramètres :
      //	sNom (chaîne ANSI) : Nom de l'option
      //	defVal : Valeur par défaut
      // Valeur de retour :
      // 	Aucune
      
      Procédure DeclareOption(LOCAL sNom est une chaine, defVal )
      // Déclare une nouvelle option (sans action si l'option a déjà été déclarée
      
      nRang est un entier = nTrouveOption(sNom)
      si nRang = -1 alors
      	gnnbOption++
      	Dimension(gptabOption,gnnbOption)
      	gptabOption[gnnbOption]:m_sNomOption = Majuscule(sNom)
      	gptabOption[gnnbOption]:m_valOption = defVal
      SINON
      	gptabOption[nRang]:m_sNomOption = Majuscule(sNom)
      	gptabOption[nRang]:m_valOption = defVal
      FIN
     type : 458752
   -
     name : nTrouveOption
     procedure_id : 4542574406889923645
     type_code : 15
     code : |1-
      // Syntaxe :
      //[ <Résultat> = ] nTrouveOption (<sNom> est chaîne)
      //
      // Paramètres :
      //	sNom (chaîne ANSI) : Nom de l'option recherchée
      // Valeur de retour :
      // 	entier : 
      
      Procédure nTrouveOption(LOCAl sNom est une chaine)
      
      nBorne est un entier
      
      // Retourne le rang d'une option ou -1 si elle n'existe pas
      nBorne = gnnbOption
      pour nRang = 1 à nBorne
      	si gptabOption[nRang]:m_sNomOption ~= sNom alors renvoyer nRang
      FIN
      
      renvoyer -1
     type : 458752
   -
     name : sGetOption
     procedure_id : 4542574840911359590
     type_code : 15
     code : |1-
      // Syntaxe :
      //[ <Résultat> = ] sGetOption (<sNom> est chaîne)
      //
      // Paramètres :
      //	sNom (chaîne ANSI) : Nom de l'option recherchée
      // Valeur de retour :
      // 	Type indéterminé : 
      
      Procédure sGetOption(LOCAL sNom est une chaine)
      
      // Retourne la valeur chaine d'une option
      nRang est un entier = nTrouveOption(sNom)
      si nRang = -1 alors renvoyer ""
      
      renvoyer "" + gptabOption[nRang]:m_valOption
     type : 458752
   -
     name : nGetOption
     procedure_id : 4542575060406613199
     type_code : 15
     code : |1+
      // Syntaxe :
      //[ <Résultat> = ] nGetOption (<sNom> est chaîne)
      //
      // Paramètres :
      //	sNom (chaîne ANSI) : Nom de l'option recherchée
      // Valeur de retour :
      // 	Type indéterminé : 
      
      Procédure nGetOption(LOCAL sNom est une chaine)
      
      // Retourne la valeur entière d'une option
      nRang est un entier = nTrouveOption(sNom)
      si nRang = -1 alors renvoyer 0
      
      renvoyer val(gptabOption[nRang]:m_valOption)
     type : 458752
   -
     name : InitOption
     procedure_id : 4542576363069200040
     type_code : 15
     code : |1-
      // Syntaxe :
      //InitOption (<sCleReg> est chaîne)
      //
      // Paramètres :
      //	sCleReg (chaîne ANSI) : Clé de registre qui contient les options globales
      // Valeur de retour :
      // 	Aucune
      
      Procédure InitOption(LOCAL sCleReg est une chaine )
      
      // Initialisation de la collection
      gsCleOption = sCleReg
     type : 458752
   -
     name : SauveOption
     procedure_id : 4542576483260351416
     type_code : 15
     code : |1-
      // Syntaxe :
      //SauveOption ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      Procédure SauveOption()
      
      
      nBorne est un entier
      
      // Sauvegarde des options dans le registre
      
      // On force la création de la clé
      RegistreCréeClé(gsCleOption)
      
      nBorne = gnnbOption
      // Puis on ajoute les valeurs (elles sont toutes sauvées au format texte)
      pour n=1 à nBorne
      	RegistreEcrit(gsCleOption, gptabOption[n]:m_sNomOption, "" + gptabOption[n]:m_valOption)
      FIN
     type : 458752
   -
     name : ChargeOption
     procedure_id : 4542577148404769295
     type_code : 15
     code : |1-
      // Syntaxe :
      //ChargeOption ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      Procédure ChargeOption()
      
      nBorne est un entier
      
      // Lecture des options depuis la base de registre
      nBorne = gnnbOption
      
      // Puis on ajoute les valeurs (elles sont toutes sauvées au format texte)
      pour n = 1 à nBorne
      	si RegistreExiste(gsCleOption,gptabOption[n]:m_sNomOption) alors
      		gptabOption[n]:m_valOption = RegistreLit(gsCleOption, gptabOption[n]:m_sNomOption)
      	fin
      FIN
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : CAAAAAgAAACVGQgYbalU7DKC3oH4ItDrjtcTHXXV/EuH8q0IbhyQlw==
resources :
 string_res :
  identifier : 0x1fc795210c229ed0
  internal_properties : CAAAAAgAAACm76HWfKGWp33VjXInA4cRlqArlgTTA862QGt72W2ld5Y=
custom_note :
 internal_properties : CAAAAAgAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
